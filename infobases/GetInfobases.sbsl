#!C:\Executor\bin\executor_j11.cmd

/*******************************************************************************
 * Copyright (c) 2022 Alexander Kapralov and Contributors
 * This program and the accompanying materials are made available under
 * the terms of the BSD 3-Clause License which is available at
 * https://spdx.org/licenses/BSD-3-Clause.html#licenseText
 *
 * SPDX-License-Identifier: BSD-3-Clause
 *
 * Contributors:
 *    Aleksandr Kapralov - Initial API and implementation
 *
 ******************************************************************************/

структура ОписаниеИБ
    знч НаименованиеСекции: Строка
    пер Connect: Строка
    знч ID: Строка
    пер OrderInList: Число
    пер Folder: Строка = "/"
    пер OrderInTree: Число
    пер External: Число = 0
    пер ClientConnectionSpeed: Строка
    пер App: Строка
    пер AppArch: Строка
    пер DefaultApp: Строка
    пер WA: Строка
    пер WSA: Строка
    пер Version: Строка
    пер DefaultVersion: Строка
    пер AdditionalParameters: Строка
    пер WebCommonInfoBaseURL: Строка
    пер HttpsCA: Строка
    пер HttpsCert: Строка
    пер HttpsCAFile: Строка
    пер HttpsCertFile: Строка
    пер HttpsCertSelect: Строка
    пер ShowInList: Число
    пер MobilePublicKey: Строка
    пер WebCommonInfoBases: Строка

    конструктор(НаименованиеСекции, ID)
;

структура Root
    знч root: Body
;

структура Body
    знч ClientID: Строка = "00000000-0000-0000-0000-000000000000"
    знч InfoBasesCheckCode: Строка
    знч InfoBases: Строка

    конструктор(InfoBasesCheckCode, InfoBases)
;


структура ОписаниеСервера
    знч АдресСервера: Строка
    пер ИмяПользователя: Строка = ""
    пер ПарольПользователя: Строка = ""
    
    конструктор (АдресСервера)
;

метод Скрипт()
    пер базыСерверов = новый Массив()

    пер сервера = новый Массив()
    сервера.Добавить(новый ОписаниеСервера("localhost"))

    для описаниеСервера из сервера
        пер администрированиеСервера = новый АдминистрированиеСервера(описаниеСервера.АдресСервера, 1545)
        администрированиеСервера.ВыполнитьАутентификацию()

        для кластер из администрированиеСервера.ПолучитьКластеры()
            кластер.ВыполнитьАутентификацию(описаниеСервера.ИмяПользователя, описаниеСервера.ПарольПользователя)

            знч наименованиеПапки = кластер.ИмяКомпьютера
            знч идПапки = Строка(кластер.ИдентификаторКластера)

            пер описаниеПапки = новый ОписаниеИБ(наименованиеПапки, идПапки)

            базыСерверов.Добавить(описаниеПапки)

            для инфобазаКластера из кластер.ПолучитьИнформационныеБазы()
                знч наименованиеСекции = инфобазаКластера.Имя
                знч ид = Строка(инфобазаКластера.ИдентификаторИнформационнойБазы)
                знч строкаСоединения = "Srvr=\"%{кластер.ИмяКомпьютера}:%{кластер.Порт}\";Ref=\"%{инфобазаКластера
                    .Имя}\";"

                пер описаниеИБ = новый ОписаниеИБ(наименованиеСекции, ид)
                описаниеИБ.Connect = строкаСоединения
                описаниеИБ.Folder = "/%{кластер.ИмяКомпьютера}"

                базыСерверов.Добавить(описаниеИБ)
            ;
        ;
    ;

    пер заголовки = новый Соответствие()
    заголовки.Вставить("Content-Type", "application/json;charset=utf-8")
    заголовки.Вставить("Cache-Control", "no-cache")

    для заголовок из заголовки
        Консоль.Записать("%{заголовок.Ключ}: %{заголовок.Значение}")
    ;

    Консоль.Записать("")

    знч идентификаторОбщегоСписка = ДатаВремя.Сейчас().Форматировать("YYYYMMddHHmmss")

    пер описаниеИБСтрокой = ""
    для описаниеИБ из базыСерверов
        описаниеИБСтрокой +=
            "[%{описаниеИБ.НаименованиеСекции}]
            Connect=%{описаниеИБ.Connect}
            ID=%{описаниеИБ.ID}
            Folder=%{описаниеИБ.Folder}
            External=%{описаниеИБ.External}
            "
    ;

    знч тело = новый Body(идентификаторОбщегоСписка, описаниеИБСтрокой)
    знч содержимое = новый Root(тело)
    знч представлениеСодержимого = СериализацияJson.ЗаписатьОбъект(содержимое)

    Консоль.Записать(представлениеСодержимого)
;